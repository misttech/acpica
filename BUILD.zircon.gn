# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# TODO(fxb/59803): There is significant duplication in this file vs the file
# "BUILD.gn". We should aim to avoid the duplication.

import("$zx/kernel/params.gni")

group("headers") {
  public_configs = [ ":headers.config" ]
}

config("headers.config") {
  visibility = [ ":headers" ]
  include_dirs = [ "include" ]

  # UBSan warns on a few instances of misaligned loads. Define this macro to
  # perform byte-by-byte accesses.
  defines = [ "ACPI_MISALIGNMENT_NOT_SUPPORTED" ]
}

static_library("acpica") {
  public_deps = [ ":headers" ]

  # Local sources use #include "acpi.h", not #include <acpica/acpi.h>.
  include_dirs = [ "source/include" ]

  # The kernel only uses a subset of the ACPICA sources.
  #
  # We explicitly list the subset here to avoid inadvertant expansion of
  # the set used.
  sources = [
    "source/components/hardware/hwregs.c",
    "source/components/hardware/hwsleep.c",
    "source/components/hardware/hwvalid.c",
    "source/components/hardware/hwxface.c",
    "source/components/hardware/hwxfsleep.c",
    "source/components/tables/tbdata.c",
    "source/components/tables/tbfadt.c",
    "source/components/tables/tbfind.c",
    "source/components/tables/tbinstal.c",
    "source/components/tables/tbprint.c",
    "source/components/tables/tbutils.c",
    "source/components/tables/tbxface.c",
    "source/components/tables/tbxfroot.c",
    "source/components/utilities/utalloc.c",
    "source/components/utilities/utascii.c",
    "source/components/utilities/utcache.c",
    "source/components/utilities/utdecode.c",
    "source/components/utilities/utexcep.c",
    "source/components/utilities/utglobal.c",
    "source/components/utilities/utinit.c",
    "source/components/utilities/utlock.c",
    "source/components/utilities/utmisc.c",
    "source/components/utilities/utmutex.c",
    "source/components/utilities/utosi.c",
    "source/components/utilities/utstring.c",
    "source/components/utilities/utxferror.c",
    "source/components/utilities/utxfinit.c",

    # Zircon OS compat library.
    "$zx/kernel/lib/acpica/oszircon.cc",
  ]

  deps = [
    "$zx/kernel/platform/pc:headers",
    "$zx/kernel/vm:headers",
  ]

  # We need to specify -fno-strict-aliasing, since ACPICA has a habit of
  # violating strict aliasing rules in some of its macros.  Rewriting this
  # code would increase the maintenance cost of bringing in the latest
  # upstream ACPICA, so instead we mitigate the problem with a compile-time
  # flag.  We take the more conservative approach of disabling
  # strict-aliasing-based optimizations, rather than disabling warnings.
  cflags = [ "-fno-strict-aliasing" ]

  # Disable the default set of warnings in third-party code.
  configs -= [ "$zx/public/gn/config:default_warnings" ]

  if (!is_gcc) {
    # TODO(41663): UBSan has found an instance of undefined behavior in this target.
    # Disable UBSan for this target temporarily until it is migrated into CI/CQ.
    cflags += [ "-fno-sanitize=undefined" ]

    # Ensure atomic accesses are not being inadvertently generated on unaligned
    # words (c.f. I638bd500ba01d5a6f38ccd598e66a6ba65460e84)
    cflags += [ "-Watomic-alignment" ]
  }
}
